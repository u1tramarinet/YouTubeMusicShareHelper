plugins {
    id 'com.android.application'
    id 'androidx.navigation.safeargs'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.android.gms.oss-licenses-plugin'
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.u1tramarinet.youtubemusicsharehelper"
        minSdk 28
        targetSdk 34
        versionCode 7
        versionName "0.0.7"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            //noinspection GroovyConstructorNamedArguments
            manifestPlaceholders = [appIcon: "@mipmap/ic_launcher", appIconRound: "@mipmap/ic_launcher_round", admobAppId: localProperties.getProperty("admob.app.id.manifest")]
            buildConfigField("String", "ADMOB_APP_ID", "\"${localProperties.getProperty("admob.app.id.layout")}\"")
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true

            //noinspection GroovyConstructorNamedArguments
            manifestPlaceholders = [appIcon: "@mipmap/ic_launcher_debug", appIconRound: "@mipmap/ic_launcher_debug_round", admobAppId: localProperties.getProperty("admob.app.id.manifest.test")]
            buildConfigField("String", "ADMOB_APP_ID", "\"${localProperties.getProperty("admob.app.id.layout.test")}\"")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    androidResources {
        generateLocaleConfig true
    }

    buildFeatures {
        dataBinding true
    }
    namespace 'com.u1tramarinet.youtubemusicsharehelper'
}

kapt {
    correctErrorTypes true
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    //noinspection KtxExtensionAvailable
    implementation "androidx.navigation:navigation-fragment:$navigation_version"
    //noinspection KtxExtensionAvailable
    implementation "androidx.navigation:navigation-ui:$navigation_version"
    //noinspection KtxExtensionAvailable
    implementation 'androidx.palette:palette:1.0.0'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation 'com.google.android.gms:play-services-ads:22.6.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.9'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

configurations.implementation {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}